## SmaCC ParserParsing converts the stream of tokens provided by the scanner into some object.By default, this object will be a parse tree, but it does not have to be that way.For example, the SmaCC tutorial shows a calculator. This calculator does not produce a parse tree; the result is interpreted on the fly.### Production RulesThe production rules contains the grammar for the parser.The first production rule is considered to be the starting rule for the parser.Each production rule consists of a non-terminal symbol name followed by a ":" separator which is followed by a list of possible productions separated by vertical bar, "|", and finally terminated by a semicolon, ";".```Expression 
	: Expression 'left' "+" 'operator' Expression 'right' {{Binary}}
	| Expression 'left' "-" 'operator' Expression 'right' {{Binary}}
	| Expression 'left' "*" 'operator' Expression 'right' {{Binary}}
	| Expression 'left' "/" 'operator' Expression 'right' {{Binary}}
	| Expression 'left' "^" 'operator' Expression 'right' {{Binary}}
	| "(" Expression ")" {{}}
	| Number
	;
Number 
	: <number> {{Number}}
	;```Each production consists of a sequence of non-terminal symbols, tokens, or keywords followed by some optional Smalltalk code enclosed in curly brackets, {} or an AST node definition enclosed in two curly brackets, \{{\}}.Non-terminal symbols are valid Smalltalk variable names and must be defined somewhere in the parser definition.Forward references are valid.Tokens are enclosed in angle brackets as they are defined in the scanner \(e.g., <token>\) and keywords are enclosed in double-quotes \(e.g., "then"\).Keywords that contain double-quotes need to have two double-quotes per each double-quote in the keyword.For example, if you need a keyword for one double-quote character, you would need to enter """" \(four double-quote characters\).The Smalltalk code is evaluated whenever that production is matched.If the code is a zero or a one argument symbol, then that method is performed.For a one argument symbol, the argument is an OrderedCollection that contains one element for each item in the production.If the code isn't a zero or one argument symbol, then the code is executed and whatever is returned by the code is the result of the production.If no Smalltalk code is specified, then the default action is to execute the #reduceFor: method \(unless you are producing an AST parser\).This method converts all items into an OrderedCollection.If one of the items is another OrderedCollection, then all of its elements are added to the new collection.Inside the Smalltalk code you can refer to the values of each production item by using literal strings.The literal string, '1', refers to the value of the first production item.The values for tokens and keywords will be SmaCCToken objects.The value for all non-terminal symbols will be whatever the Smalltalk code evaluates to for that non-terminal symbol.### Named SymbolsWhen entering the Smalltalk code, you can get the value for a symbol by using the literal strings \(e.g., '2'\).However, this creates difficulties when modifying a grammar.If you insert some symbol at the beginning of a production, then you will need to modify your Smalltalk code changing all literal string numbers.Instead you can name each symbol in the production and then refer to the name in the Smalltalk code.To name a symbol \(non-terminal, token, or keyword\), you need to add a quoted variable name after the symbol in the grammar.For example, "MySymbol : Expression 'expr' "+" <number> 'num' {expr + num} ;" creates two named variables: one for the non-terminal Expression and one for the <number> token.These variables are then used in the Smalltalk code.### Error RecoveryNormally, when the parser encounters an error, it raises the SmaCCParserError exception and parsing is immediately stopped.However, there are times when you may wish to try to parse more of the input.For example, if you are highlighting code, you do not want to stop highlighting at the first syntax error.Instead you may wish to attempt to recover after the statement separator -- the period ".".SmaCC uses the error symbol to specify where error recovery should be attempted.For example, we may have the following rule to specify a list of Smalltalk statements:```Statements : Expression | Statements "." Expression ;```If we wish to attempt recovery from a syntax error when we encounter a period, we can change our rule to be:```Statements : Expression | Statements "." Expression | error "." Expression ;```While the error recovery allows you to proceed parsing after a syntax error, it will not allow you to return a parse tree from the input.Once the input has been parsed with errors, it will raise a non-resumable SmaCCParserError.### Shortcuts@sec:shortcutsExtended BNF grammars extend the usual notation for grammar productions with some convenient shortcuts.SmaCC supports the common notations of Kleene star \(`*`\) for 0 or more, question mark \(`?`\) for 0 or 1, and Kleene plus \(`+`\) for 1 or more repetitions of the preceding item.For example, rather than specifying a `ParameterList` in the conventional way, like this```<name> : [a-zA-Z] [a-zA-Z0-9_']* ;
<whitespace>: \s+ ;

ParameterList
	: Parameter
	| ParameterList Parameter
	;
	
Parameter
	: <name>
	;```we can be more concise and specify it like this:```<name> : [a-zA-Z] [a-zA-Z0-9_']* ;
<whitespace>: \s+ ;

ParameterList
	: Parameter +
	;
	
Parameter
	: <name>
	;```If we are generating an AST, these shortcuts have the aditional advantage of producing more compact AST nodes.For more information, see the Chapter on *@cha:idioms@*.